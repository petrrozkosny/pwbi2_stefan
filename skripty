01 ruseni filtru = 
CALCULATE(
SUM(data[Srazky]),ALL(data[rok]))

______________________________________

02 pridani filtru = 
-- TAKHLE NE
VAR _1 = 
CALCULATE(
    SUM(data[Srazky]),
    FILTER(data,
    data[Lokalita] = "RUZYNE")
)
-- TAKHLE ANO, POKUD NEFUNGUJE _3
VAR _2 = 
CALCULATE(
    SUM(data[Srazky]),
    FILTER(
        ALL(data[Lokalita]),
    data[Lokalita] = "RUZYNE"))

-- TAKHLE ANO
VAR _3 = 
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE")
RETURN _3
____________________________________________

19 minimalni a maximalni mesicni srazky = 
-- tabulka srazek pro jednotlive roky daneho mesice
-- respektujeme filtr mesice daneho vizualem
VAR _tabulka = 
    ADDCOLUMNS(
    ALL(dim_datumy[rok]),
    "suma srazek",
    CALCULATE(SUM(data[Srazky])))
-- minimalni a maximalni hodnoty ze sloupce suma srazek vlastni tabulky
-- vytvorene v promenne _tabulka
VAR _minimalni_srazky = MINX(_tabulka,[suma srazek])
VAR _maximalni_srazky = MAXX(_tabulka,[suma srazek])
RETURN
    -- testujeme vice podminek, hledame podminku vracejici TRUE()
    SWITCH(TRUE(),
    SUM(data[Srazky]) = _minimalni_srazky,"GOLD",
    SUM(data[Srazky]) = _maximalni_srazky,"CYAN")

________________________________________________

24 mesice s nejvetsim poctem dnu bez srazek ruzyne = 
-- v danem roce
VAR _tabulka = 
    ADDCOLUMNS(
        ALL(data[mesic]),
    "pocet dnu bez srazek",
    CALCULATE(
        COUNT(data[Datum]),
        data[Srazky] = 0,
        NOT ISBLANK(data[Srazky])))
VAR max_pocet_vyskytu = MAXX(_tabulka,[pocet dnu bez srazek])
RETURN
    CONCATENATEX(
        FILTER(_tabulka,
        [pocet dnu bez srazek] = max_pocet_vyskytu),
    data[mesic],", ")

_____________________________________________

28 datumy maximalnich srazek lokalita = 

CONCATENATEX(
    FILTER(data,data[Srazky] = MAX(data[Srazky])),
data[Datum],", ",data[Datum],ASC)

_______________________________________________

29 roky nejvice dnu bez srazek = 

VAR _tabulka = 
ADDCOLUMNS(
    ALL(dim_datumy[rok]),
"pocet dnu bez srazek",
CALCULATE(COUNT(data[Datum]),data[Srazky] = 0 && NOT ISBLANK(data[Srazky])))
VAR max_pocet_dnu = MAXX(_tabulka,[pocet dnu bez srazek])
RETURN
CONCATENATEX(
    FILTER(_tabulka,[pocet dnu bez srazek] = max_pocet_dnu),
dim_datumy[rok],", ")
______________________________________________

30 text tlacitka = 

IF(
    HASONEFILTER(dim_lokality[Lokalita]),
    "Přejít na detail lokality "&SELECTEDVALUE(dim_lokality[Lokalita]),
    "Kliknutím do tabulky vyber lokalitu")

_______________________________________

33 datumy max srazek = 

VAR max_srazky = MAX(data[Srazky])

VAR datumy_max_srazek = 
ADDCOLUMNS(
FILTER(data,data[Srazky] = max_srazky),
"datum-hodnota",
data[Datum] &" | "& data[Srazky])

RETURN
CONCATENATEX(
    datumy_max_srazek,[datum-hodnota],UNICHAR(10))

____________________________________

08 minimalni datum maximalni teploty v roce = 
if(ISFILTERED(dim_datumy[rok]),
-- v dane lokalite, zemi, roce
CALCULATE(
    MIN(data[Datum]),
    FILTER(ALL(data[Teplota]),
    data[Teplota] = 
    -- maximalni teplota v dane zemi, lokalite, roce
    MAX(data[Teplota])
)),"jsem v totalu, nepočítám")


_____________________________________________________


36 nejsrazkovejsi roky lokality = 
VAR _tabulka = 
ADDCOLUMNS(
    ALL(dim_datumy[rok]),
"suma srazek",
    CALCULATE(SUM(data[Srazky])))
VAR _maximum = MAXX(_tabulka,[suma srazek])
RETURN
CONCATENATEX(
    FILTER(_tabulka,[suma srazek] = _maximum),
dim_datumy[rok],", ")

___________________________________________


40 suma srazek TOP3 lokality = 


VAR _tabulka = 
ADDCOLUMNS(
ALL(dim_lokality[Lokalita]),
"suma srazek",
CALCULATE(SUM(data[Srazky])))

VAR _tabulka_rank = 
ADDCOLUMNS(_tabulka,
"rank",
RANKX(
    _tabulka,[suma srazek],,DESC))

RETURN 
SUMX(
    FILTER(
ADDCOLUMNS(
    _tabulka_rank,
    "typ",
    IF([rank]<=3,dim_lokality[Lokalita],"ostatní")),
    [typ] <> "ostatní"),
[suma srazek])

_____________________________________ _______

41 pf topn srazek = 
VAR _tabulka = 
ADDCOLUMNS(
    FILTER(
    CALCULATETABLE(
        ALL(data[rok],data[mesic],data[Lokalita]),
        -- timto se zbavujeme filtru v rozsirene tabulce
        ALL(dim_datumy[mesic],dim_datumy[rok]),ALL(dim_lokality[Lokalita])),-- nebo ALL(data)
    data[Lokalita] IN VALUES(dim_lokality[Lokalita]))
    ,"suma srazek",
    -- funkce CALCULATE() jde radek po radku a pocita sumu srazek. Prochazi kazdy jeden radek
    -- tabulky vracene funkci ALL(), ale pta (agreguje) se do tabulek filtrovanych vizualem,
    -- proto znovu musíme použít modifikátor filtru, tj. funkci ALL()
    CALCULATE(SUM(data[Srazky]),ALL(dim_datumy[mesic],dim_datumy[rok]),ALL(dim_lokality[Lokalita])))

-- tabulka s dopocitanym sloupcem rank srazek
VAR _tabulka_rank = 
ADDCOLUMNS(
    _tabulka,
"rank srazek",
RANKX(_tabulka,[suma srazek],,DESC))

-- v danem roce, lokalite, mesici
VAR suma_srazek = SUM(data[Srazky])

VAR rank_suma_srazek =
-- zde se ptame na maximalni rank dane sumy srazek. Protoze kazda suma srazek ma jediny rank
-- tak je jedno, jestli se ptame pres MAXX(), MINX(), AVERAGEX()
MAXX(
    FILTER(_tabulka_rank,[suma srazek] = suma_srazek),
[rank srazek])
RETURN
if(
    rank_suma_srazek
    <=topn_hodnoty[Hodnota parametru topn_hodnoty]
    -- pokud se rank sumy srazek v dane bunce rovna ci je mensi ranku vybraneho prurezem
    -- vracime barvu gold
    ,"gold")
___________________________________________

42 TOP3 teploty = 
CONCATENATEX(
FILTER(
ADDCOLUMNS(
    ALL(data[Teplota]),
    "rank teploty",
    RANKX(ALL(data[Teplota]),CALCULATE(MAX(data[Teplota])),,DESC)
),
[rank teploty] <=3),data[Teplota],", ",[rank teploty],ASC)
